import sys


Import("vars")

platform_sources = ["main.c", "plat.c"]
py_sources = Split("""main.py
    #src/lib/__bi.py 
    #src/lib/sys.py 
    #src/lib/ipm.py
    #src/lib/string.py
    #src/lib/list.py 
    #src/lib/dict.py 
    """)

cflags = "-g -ggdb -D__DEBUG__=1 -Os -Wall -gstabs -Wstrict-prototypes -Werror"

env = Environment(variables = vars,
    CPPPATH = [".", "#src/vm", "#src/platform/desktop/build"],
    CCFLAGS = cflags,)
if not sys.platform.startswith("win"):
    env['ARFLAGS'] = "rcs"

pmfeatures = Command("pmfeatures.h", "pmfeatures.py",
    "src/tools/pmGenPmFeatures.py $SOURCE > $TARGET")
pmvm_lib = SConscript("#src/vm/SConscript", "env", variant_dir = "vm")
pyco_sources = Command("pyco_sources.c", py_sources,
    "src/tools/pmCoCreator.py ../pmfeatures.py $SOURCES > $TARGET")
main = env.Program("main", platform_sources + pmvm_lib + pyco_sources)
Return("main")

#:mode=python:
